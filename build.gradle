plugins {
    id 'java'
    id 'eclipse'
    id "org.jetbrains.gradle.plugin.idea-ext" version "1.1.10"
    id("xyz.jpenilla.run-velocity") version "2.3.1"
    id "com.gradleup.shadow" version "8.3.7"
}

group = 'rs.onako2'
version = '1.1.1'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "open-collab"
        url = "https://repo.opencollab.dev/main/"
    }
}

dependencies {
    compileOnly "com.velocitypowered:velocity-api:3.4.0-SNAPSHOT"
    annotationProcessor "com.velocitypowered:velocity-api:3.4.0-SNAPSHOT"
    compileOnly "com.velocitypowered:velocity-proxy:3.4.0-SNAPSHOT"
    compileOnly "org.yaml:snakeyaml:2.4"
    compileOnly "org.geysermc.geyser:api:2.7.0-SNAPSHOT"
    compileOnly "org.geysermc.geyser:core:2.7.0-SNAPSHOT"
    implementation "org.bstats:bstats-velocity:3.0.2"
}

tasks.shadowJar {
    relocate("org.bstats", "rs.onako2.redirectplayers.bstats")
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

def templateSource = file('src/main/templates')
def templateDest = layout.buildDirectory.dir('generated/sources/templates')
def generateTemplates = tasks.register('generateTemplates', Copy) { task ->
    def props = [
            'version': project.version
    ]
    task.inputs.properties props

    task.from templateSource
    task.into templateDest
    task.expand props
}

sourceSets.main.java.srcDir(generateTemplates.map { it.outputs })

rootProject.idea.project.settings.taskTriggers.afterSync generateTemplates
project.eclipse.synchronizationTasks(generateTemplates)

tasks {
    runVelocity {
        // Configure the Velocity version for our task.
        // This is the only required configuration besides applying the plugin.
        // Your plugin's jar (or shadowJar if present) will be used automatically.
        velocityVersion("3.4.0-SNAPSHOT")
    }
}